USE CLARITY

--To test
--DECLARE 
-- @in_StartDate DATE,
-- @in_EndDate DATE,
-- @in_Locs VARCHAR(4000),
-- @in_Blocks VARCHAR(4000),
-- @BlkTemplate INT,
-- @SchUtil INT,
-- @PerfUtil INT,
-- @PedsVersion INT;

--SET @in_StartDate = '10/01/2017';
--SET @in_EndDate = '10/31/2017';
--SET @in_Locs = '1071024300';--,1071024303,1071024400,1071035400'
--SET @in_Blocks = '0';-- ,10,30,230'
--SET @BlkTemplate = 1; 
--SET @SchUtil = 3;
--SET @PerfUtil = 2; 
--SET @PedsVersion = 1;

CREATE PROCEDURE [Rptg].[uspSrc_block_utilization]
(
@in_StartDate DATETIME,
@in_EndDate DATETIME,
@in_Locs VARCHAR(MAX),
@in_Blocks VARCHAR(MAX),
@BlkTemplate INT,
@PerfUtil VARCHAR(MAX),
@SchUtil VARCHAR(MAX),
@PedsVersion INT
)
AS

SET NOCOUNT ON 

/* CREATE LOCAL TABLE VARIABLES TO HOLD VALUES OF MULTIPLE-VALUED PARAMETERS */
DECLARE @tab_Locs TABLE (LOC_ID NUMERIC(18))
	INSERT INTO @tab_Locs SELECT Param FROM ETL.fn_ParmParse(@in_Locs,',')
DECLARE @tab_Blocks TABLE (BLOCK_ID VARCHAR(66))
	INSERT INTO @tab_Blocks SELECT Param FROM ETL.fn_ParmParse(@in_Blocks,',')

	
BEGIN IF OBJECT_ID('tempdb..#AVAIL') IS NOT NULL  DROP TABLE #AVAIL END;
BEGIN IF OBJECT_ID('tempdb..#RELD') IS NOT NULL  DROP TABLE #RELD end; 
BEGIN IF OBJECT_ID('tempdb..#BLKRLSD') IS NOT NULL  DROP TABLE #BLKRLSD end;
BEGIN IF OBJECT_ID('tempdb..#MANRLSD') IS NOT NULL  DROP TABLE #MANRLSD end;
BEGIN IF OBJECT_ID('tempdb..#BUTIL') IS NOT NULL  DROP TABLE #BUTIL end;

/*
SELECT * FROM #AVAIL WHERE LOCATION='MAIN'
SELECT * FROM #RELD
SELECT * FROM #BLKRLSD
SELECT * FROM #MANRLSD
SELECT * FROM #BUTIL
*/

/* FIND AVAILABLE MINUTES */
SELECT
 MAX(b.LOCATION) LOCATION
,MAX(b.LOCATION_FULL_NAME) LOCATION_FULL_NAME
,MAX(b.BLOCK_NAME) AS BLOCK_NAME
,MAX(b.RAW_BLOCK_NAME) RAW_BLOCK_name
,b.BLOCK_ID
,MAX(b.RAW_BLOCK_ID) RAW_BLOCK_ID
,MAX(b.PEDIATRIC_BLOCK_ID) PEDIATRIC_BLOCK_ID
,MAX(b.PEDIATRIC_BLOCK_NAME) PEDIATRIC_BLOCK_NAME
,b.LOC_ID
,SUM(b.SLOT_LENGTH) AVAILABLE_MINUTES
INTO #AVAIL
FROM (
	SELECT
	oub.SNAPSHOT_DATE
	,loc.LOCATION_ABBR LOCATION
	,loc.LOC_NAME LOCATION_FULL_NAME
	,sgsvc.LOC_ID
	,oub.SLOT_TYPE
	,coalesce((CASE WHEN @PedsVersion= 2 and oub.SERVICE_C IN ('260','270','280','290','300','310','320','330','500','600','610','620','630')  then '330' ELSE oub.SERVICE_C END) ,oub.GROUP_ID,oub.SURGEON_ID,'UNBLOCKED') BLOCK_ID
	,coalesce((CASE WHEN @PedsVersion= 1 and oub.SERVICE_C IN ('260','270','280','290','300','310','320','330','500','600','610','620','630')  then '330' ELSE oub.SERVICE_C END) ,oub.GROUP_ID,oub.SURGEON_ID,'UNBLOCKED') PEDIATRIC_BLOCK_ID
	,coalesce(oub.SERVICE_C,oub.GROUP_ID,oub.SURGEON_ID,'UNBLOCKED') RAW_BLOCK_ID
	,svc.NAME SERVICE_BLOCK
	,oub.ROOM_ID
	,oub.SLOT_LENGTH
	,rm.PROV_NAME ROOM_NAME
	,COALESCE((CASE WHEN @PedsVersion= 2 and oub.SERVICE_C IN ('260','270','280','290','300','310','320','330','500','600','610','620','630')  then 'Pediatrics' ELSE svc.NAME end), ser_sgn_blk.PROV_NAME, ogp.GROUP_NAME,'z_UNBLOCKED') BLOCK_NAME
	,COALESCE((CASE WHEN @PedsVersion= 1 and oub.SERVICE_C IN ('260','270','280','290','300','310','320','330','500','600','610','620','630')  then 'Pediatrics' ELSE svc.NAME end), ser_sgn_blk.PROV_NAME, ogp.GROUP_NAME,'z_UNBLOCKED') PEDIATRIC_BLOCK_NAME
	,COALESCE( svc.NAME,ser_sgn_blk.PROV_NAME, ogp.GROUP_NAME,'z_UNBLOCKED')  RAW_BLOCK_NAME
	FROM CLARITY.dbo.OR_UTIL_BLOCK oub
	LEFT OUTER JOIN CLARITY.dbo.ZC_OR_SERVICE svc ON oub.SERVICE_C = svc.SERVICE_C
	LEFT OUTER JOIN CLARITY.dbo.OR_GRP ogp ON oub.GROUP_ID = ogp.GROUP_ID
	LEFT OUTER JOIN CLARITY.dbo.CLARITY_SER rm ON oub.ROOM_ID = rm.PROV_ID
	LEFT OUTER JOIN CLARITY.dbo.CLARITY_SER ser_sgn_blk ON oub.SURGEON_ID = ser_sgn_blk.PROV_ID
	LEFT OUTER JOIN CLARITY.dbo.OR_SER_SURG_SRVC sgsvc ON oub.ROOM_ID = sgsvc.PROV_ID
	LEFT OUTER JOIN CLARITY.dbo.CLARITY_LOC loc on sgsvc.LOC_ID = loc.LOC_ID
	LEFT OUTER JOIN CLARITY.dbo.DATE_DIMENSION dd ON oub.SNAPSHOT_DATE = dd.CALENDAR_DT
	WHERE 1=1 
	AND oub.SNAPSHOT_NUMBER = @BlkTemplate
	AND oub.SLOT_TYPE IN ('BLOCK')
	AND dd.HOLIDAY_YN = 'N'
	AND dd.WEEKEND_YN = 'N'
	AND oub.SNAPSHOT_DATE >= @in_StartDate
	AND oub.SNAPSHOT_DATE < DATEADD(D,1,@in_EndDate)
	AND sgsvc.LOC_ID in (1071024300--UVHE Main OR
	, 1071035400 --UVBB Outpatient Surgery Center
	, 1071041900 --OCIR IVY ROAD OR
	, 1071036300 /*CVSL SURGICAL CARE RIVERSIDE OR*/
	, 1071029500 /*CPSA OR*/
	, 1071038800 /*PWMC OR*/
	, 1071074300 /*HYMC OR*/
	, 1071035402 /*CPSN OPSC OR*/  /*Added 6/6/2024 - Chris Meshes*/)
	AND (0 IN (SELECT LOC_ID from @tab_Locs) OR sgsvc.LOC_ID in (SELECT LOC_ID from @tab_Locs))
	AND ('0' IN (SELECT BLOCK_ID from @tab_Blocks) OR coalesce(oub.SERVICE_C,oub.GROUP_ID,oub.SURGEON_ID) IN (SELECT BLOCK_ID from @tab_Blocks))
	)b
WHERE 1=1 
GROUP BY b.LOC_ID ,b.BLOCK_ID

--SELECT * FROM #AVAIL order by 2

/* FIND MANUAL RELEASED MINUTES */
SELECT
 MAX(b.LOCATION) LOCATION
,max(b.RAW_BLOCK_NAME) AS RAW_BLOCK_NAME
,MAX(b.BLOCK_NAME) BLOCK_NAME
,b.BLOCK_ID 
,MAX(RAW_BLOCK_ID) RAW_BLOCK_ID
,sum(b.SLOT_LENGTH) RELEASED_LENGTH_SUM
INTO #MANRLSD
FROM (
	SELECT
	 loc.LOCATION_ABBR AS LOCATION
	,oub.SNAPSHOT_DATE
	,oub.SLOT_TYPE
	,coalesce(oub.SERVICE_C,oub.GROUP_ID,oub.SURGEON_ID,'UNBLOCKED') RAW_BLOCK_ID
	,COALESCE(svc.NAME, ser_sgn_blk.PROV_NAME, ogp.GROUP_NAME,'z_UNBLOCKED') RAW_BLOCK_NAME
	,coalesce((CASE WHEN @PedsVersion= 2 and oub.SERVICE_C IN ('260','270','280','290','300','310','320','330','500','600','610','620','630')  then '330' ELSE oub.SERVICE_C END) ,oub.GROUP_ID,oub.SURGEON_ID,'UNBLOCKED') BLOCK_ID
	,COALESCE((CASE WHEN @PedsVersion= 2 and oub.SERVICE_C IN ('260','270','280','290','300','310','320','330','500','600','610','620','630')  then 'Pediatrics' ELSE svc.NAME end), ser_sgn_blk.PROV_NAME, ogp.GROUP_NAME,'z_UNBLOCKED') BLOCK_NAME
	,rm.PROV_NAME ROOM_NAME
	,oub.ROOM_ID
	,oub.SLOT_LENGTH
	FROM CLARITY.dbo.OR_UTIL_BLOCK oub
	LEFT OUTER JOIN CLARITY.dbo.ZC_OR_SERVICE svc ON oub.SERVICE_C = svc.SERVICE_C
	LEFT OUTER JOIN CLARITY.dbo.OR_GRP ogp ON oub.GROUP_ID = ogp.GROUP_ID
	LEFT OUTER JOIN CLARITY.dbo.CLARITY_SER rm ON oub.ROOM_ID = rm.PROV_ID
	LEFT OUTER JOIN CLARITY.dbo.CLARITY_SER ser_sgn_blk ON oub.SURGEON_ID = ser_sgn_blk.PROV_ID
	LEFT OUTER JOIN CLARITY.dbo.OR_SER_SURG_SRVC sgsvc ON oub.ROOM_ID = sgsvc.PROV_ID
	LEFT OUTER JOIN CLARITY.dbo.CLARITY_LOC loc on sgsvc.LOC_ID = loc.LOC_ID
	LEFT OUTER JOIN CLARITY.dbo.DATE_DIMENSION dd ON oub.SNAPSHOT_DATE = dd.CALENDAR_DT
	WHERE 1=1 
	AND oub.SNAPSHOT_NUMBER =@BlkTemplate
	AND oub.SLOT_TYPE IN ('MANREL')
	AND oub.SNAPSHOT_DATE >=@in_StartDate
	AND oub.SNAPSHOT_DATE < DATEADD(D,1,@in_EndDate)
	AND sgsvc.LOC_ID in (1071024300--UVHE Main OR
	, 1071035400 --UVBB Outpatient Surgery Center
	, 1071041900 --OCIR IVY ROAD OR
	, 1071036300 /*CVSL SURGICAL CARE RIVERSIDE OR*/
	, 1071029500 /*CPSA OR*/
	, 1071038800 /*PWMC OR*/
	, 1071074300 /*HYMC OR*/
	, 1071035402 /*CPSN OPSC OR*/  /*Added 6/6/2024 - Chris Meshes*/)
	AND ('0' IN (SELECT BLOCK_ID from @tab_Blocks) OR coalesce(oub.SERVICE_C,oub.GROUP_ID,oub.SURGEON_ID) IN (SELECT BLOCK_ID from @tab_Blocks))
	AND (0 IN (SELECT LOC_ID from @tab_Locs) OR sgsvc.LOC_ID in (SELECT LOC_ID from @tab_Locs))
	AND dd.HOLIDAY_YN = 'N'
	AND dd.WEEKEND_YN = 'N'
	)b
WHERE 1=1 
GROUP BY b.LOCATION ,b.BLOCK_ID

/* FIND AUTOMATIC RELEASE MINUTES */
SELECT
 loc.LOCATION_ABBR LOCATION
,blkrm.PROV_NAME ROOM
,blkname.BLOCK_NAME RAW_BLOCK_NAME
,COALESCE((CASE WHEN @PedsVersion= 2 and ota.FROM_BLOCK_TYPE_C = 1 and ota.FROM_BLOCK_ID  IN ('260','270','280','290','300','310','320','330','500','600','610','620','630')  then 'Pediatrics' ELSE blkname.BLOCK_NAME end),blkname.BLOCK_NAME,'z_UNBLOCKED') BLOCK_NAME
,blktyp.TITLE BLOCK_TYPE
,modtype.TITLE RELEASE_TYPE
,CAST(ota.BLOCK_START_INST AS DATE) BLOCK_DAY
,CAST(ota.MOD_INST AS DATE) RELEASED_DAY
,ota.REL_DAYS_IN_ADVANC NUM_DAYS_ADVANCE_RELEASE
,CASE 
	  WHEN blksvc.LOC_ID IN (1071024300,1071024303) AND ota.REL_DAYS_IN_ADVANC < 12 THEN 'ACCOUNTABLE - MAIN_AND_IMRIS (< 12 DAYS)'
	  WHEN blksvc.LOC_ID = 1071035400 AND ota.REL_DAYS_IN_ADVANC < 21 THEN 'ACCOUNTABLE - OPSS (<21 DAYS)'
      ELSE 'NOT ACCOUNTABLE' END AS ACCOUNTABLE_RELEASE
,ota.BLOCK_START_INST
,ota.BLOCK_END_INST
,CASE WHEN modtype.TITLE = 'AUTOMATIC BLOCK RELEASE' THEN 'SYSTEM' ELSE reluser.NAME END AS RELEASED_BY
,DATEDIFF(n,ota.BLOCK_START_INST,ota.BLOCK_END_INST) RELEASED_MINUTES
,ota.MOD_INST RELEASED_DATE_TIME
,to_blk_type.TITLE RELEASED_TO_BLOCK_TYPE
,COALESCE(to_blk.BLOCK_NAME,'N/A') RELEASED_TO_BLOCK
,ota.RECORD_ID BLOCK_INTERNAL_ID
,dd.DAY_OF_WEEK BLOCK_DAY_OF_WEEK
INTO #BLKRLSD
FROM CLARITY.dbo.OR_OTA ota
LEFT OUTER JOIN CLARITY.dbo.CLARITY_SER blkrm ON ota.SER_RECORD_ID = blkrm.PROV_ID
LEFT OUTER JOIN CLARITY.dbo.BLOCK blkname ON ota.FROM_BLOCK_ID = COALESCE(blkname.SURGICAL_SERVICE_C,blkname.SURGEON_GROUP_ID,blkname.PROVIDER_ID) AND ota.FROM_BLOCK_TYPE_C =blkname.BLOCK_TYPE_C
LEFT OUTER JOIN CLARITY.dbo.CLARITY_EMP reluser ON ota.MOD_USER_ID = reluser.USER_ID
LEFT OUTER JOIN CLARITY.dbo.OR_SER_SURG_SRVC blksvc ON blkrm.PROV_ID = blksvc.PROV_ID
LEFT OUTER JOIN CLARITY.dbo.CLARITY_LOC loc ON blksvc.LOC_ID = loc.LOC_ID
LEFT OUTER JOIN CLARITY.dbo.BLOCK to_blk ON ota.TO_BLOCK_ID = COALESCE(to_blk.SURGICAL_SERVICE_C,to_blk.SURGEON_GROUP_ID,to_blk.PROVIDER_ID)
LEFT OUTER JOIN CLARITY.dbo.ZC_OR_BLOCK blktyp ON ota.FROM_BLOCK_TYPE_C = blktyp.BLOCK_TYPE_C
LEFT OUTER JOIN CLARITY.dbo.ZC_MOD_TYPE modtype ON ota.MOD_TYPE_C = modtype.MOD_TYPE_C
LEFT OUTER JOIN CLARITY.dbo.ZC_OR_BLOCK to_blk_type ON ota.TO_BLOCK_TYPE_C = to_blk_type.BLOCK_TYPE_C
LEFT OUTER JOIN CLARITY.dbo.DATE_DIMENSION dd on ota.TEMPLATE_DT = dd.CALENDAR_DT
WHERE ota.TEMPLATE_DT >= @in_StartDate
AND ota.TEMPLATE_DT < DATEADD(d,1,@in_EndDate)
AND blksvc.LOC_ID in (1071024300--UVHE Main OR
	, 1071035400 --UVBB Outpatient Surgery Center
	, 1071041900 --OCIR IVY ROAD OR
	, 1071036300 /*CVSL SURGICAL CARE RIVERSIDE OR*/
	, 1071029500 /*CPSA OR*/
	, 1071038800 /*PWMC OR*/
	, 1071074300 /*HYMC OR*/
	, 1071035402 /*CPSN OPSC OR*/  /*Added 6/6/2024 - Chris Meshes*/)
AND (0 IN (SELECT LOC_ID from @tab_Locs) OR blksvc.LOC_ID in (SELECT LOC_ID from @tab_Locs))
AND ('0' IN (SELECT BLOCK_ID from @tab_Blocks) OR ota.FROM_BLOCK_ID IN (SELECT BLOCK_ID from @tab_Blocks))
ORDER BY blksvc.LOC_ID , ota.TEMPLATE_DT

select 
 br.LOCATION
,br.BLOCK_NAME
,SUM(CASE WHEN br.RELEASE_TYPE = 'MANUAL BLOCK RELEASE' THEN (br.RELEASED_MINUTES) ELSE 0 END) AS SUM_MANUAL_RELEASED_MINUTES
,SUM(br.RELEASED_MINUTES)  as RELEASED_MINUTES
INTO #RELD
from #BLKRLSD br
GROUP BY 
 br.LOCATION
,br.BLOCK_NAME
order by 1, 2


/* GATHER UTILIZATION DATA FOR EACH BLOCK */
SELECT CAST(oub.SNAPSHOT_DATE AS DATE) SNAPSHOT_DATE
,coalesce(oub.SERVICE_C,oub.GROUP_ID,oub.SURGEON_ID,'UNBLOCKED') RAW_BLOCK_ID
,coalesce((CASE WHEN @PedsVersion= 2 and oub.SERVICE_C IN ('260','270','280','290','300','310','320','330','500','600','610','620','630')  then '330' ELSE oub.SERVICE_C END) ,oub.GROUP_ID,oub.SURGEON_ID,'UNBLOCKED') BLOCK_ID
,datepart(day, oub.START_TIME) Start_time_day
,COALESCE(oub.CASE_ID, oub.LOG_ID) SURGERY_ID
,oub.SNAPSHOT_NUMBER
,oub.SLOT_TYPE
,oub.PROC_TIME
,CASE WHEN COALESCE(oub.CASE_ID, oub.LOG_ID) IS NULL THEN ''
	  WHEN oub.SLOT_TYPE <> 'CORRECT' THEN ''
	  WHEN oub.PROC_TIME = 'Y' THEN 'PROC TIME'
	  WHEN oub.PROC_TIME = 'N' THEN 'SETUP TIME'
	  ELSE 'N/A' END PROC_OR_SETUP_TIME
,cast(oub.START_TIME as time) START_TIME
,CAST(oub.END_TIME as time) END_TIME
,n.NAME + ' [' + CAST(oub.SNAPSHOT_NUMBER AS VARCHAR(2))  + ']' AS "SNAPSHOT" --_TYPE
,oub.CASE_ID
,oub.LOG_ID
/* SCHEDULED BELOW */
,CASE WHEN oub.SNAPSHOT_NUMBER = @SchUtil and oub.PROC_TIME ='Y' and oub.SLOT_TYPE = 'CORRECT' AND oub.CASE_ID IS NOT NULL THEN oub.SLOT_LENGTH ELSE 0 END AS SHEDULED_IN_PROC_PROCEDURE_TIME /* INCLUDES TURNOVER TIME */
,CASE WHEN oub.SNAPSHOT_NUMBER = @SchUtil and oub.PROC_TIME ='N' AND oub.SLOT_TYPE = 'CORRECT' AND oub.CASE_ID IS NOT NULL THEN oub.SLOT_LENGTH else 0 end AS SHEDULED_IN_BLOCK_SETUP_TIME

,CASE WHEN oub.SNAPSHOT_NUMBER = @SchUtil and oub.PROC_TIME ='Y' AND oub.SLOT_TYPE = 'OVERBOOK' AND oub.CASE_ID IS NOT NULL THEN oub.SLOT_LENGTH else 0 end  AS SHEDULED_OVERBOOK_PROCEDURE_TIME
,CASE WHEN oub.SNAPSHOT_NUMBER = @SchUtil and oub.PROC_TIME ='N' AND oub.SLOT_TYPE = 'OVERBOOK' AND oub.CASE_ID IS NOT NULL THEN oub.SLOT_LENGTH else 0 end  AS SHEDULED_OVERBOOK_SETUP_TIME

,CASE WHEN oub.SNAPSHOT_NUMBER = @SchUtil and oub.PROC_TIME ='Y' AND oub.SLOT_TYPE = 'OUTSIDE' AND oub.CASE_ID IS NOT NULL THEN oub.SLOT_LENGTH else 0 end  AS SHEDULED_OUT_OF_BLOCK_PROCEDURE_TIME
,CASE WHEN oub.SNAPSHOT_NUMBER = @SchUtil and oub.PROC_TIME ='N' AND oub.SLOT_TYPE = 'OUTSIDE' AND oub.CASE_ID IS NOT NULL THEN oub.SLOT_LENGTH else 0 end  AS SHEDULED_OUT_OF_BLOCK_SETUP_TIME

/* ACTUAL BELOW */
,CASE WHEN oub.SNAPSHOT_NUMBER = @PerfUtil and oub.PROC_TIME ='Y' and oub.SLOT_TYPE = 'CORRECT' AND oub.LOG_ID IS NOT NULL THEN oub.SLOT_LENGTH ELSE 0 END AS ACTUAL_IN_PROC_PROCEDURE_TIME
,CASE WHEN oub.SNAPSHOT_NUMBER = @PerfUtil and oub.PROC_TIME ='N' and oub.SLOT_TYPE = 'CORRECT' AND oub.LOG_ID IS NOT NULL THEN oub.SLOT_LENGTH ELSE 0 END AS ACTUAL_IN_BLOCK_SETUP_TIME

,CASE WHEN oub.SNAPSHOT_NUMBER = @PerfUtil and oub.PROC_TIME ='Y' and oub.SLOT_TYPE = 'OVERBOOK' AND oub.LOG_ID IS NOT NULL THEN oub.SLOT_LENGTH ELSE 0 END AS ACTUAL_OVERBOOK_PROCEDURE_TIME
,CASE WHEN oub.SNAPSHOT_NUMBER = @PerfUtil and oub.PROC_TIME ='N' and oub.SLOT_TYPE = 'OVERBOOK' AND oub.LOG_ID IS NOT NULL THEN oub.SLOT_LENGTH ELSE 0 END AS ACTUAL_OVERBOOK_SETUP_TIME

,CASE WHEN oub.SNAPSHOT_NUMBER = @PerfUtil and oub.PROC_TIME ='Y' and oub.SLOT_TYPE = 'OUTSIDE' AND oub.LOG_ID IS NOT NULL THEN oub.SLOT_LENGTH ELSE 0 END AS ACTUAL_OUT_OF_BLOCK_PROCEDURE_TIME
,CASE WHEN oub.SNAPSHOT_NUMBER = @PerfUtil and oub.PROC_TIME ='N' and oub.SLOT_TYPE = 'OUTSIDE' AND oub.LOG_ID IS NOT NULL THEN oub.SLOT_LENGTH ELSE 0 END AS ACTUAL_OUT_OF_BLOCK_SETUP_TIME


,oub.SLOT_LENGTH
,CASE WHEN oub.GROUP_ID IS NOT NULL THEN 'GROUP BLOCK'
	  WHEN oub.SURGEON_ID is not null then 'SURGEON BLOCK' 
	  WHEN oub.SERVICE_C IS NOT NULL THEN 'SERVICE BLOCK'
	  ELSE 'UNKNOWN' END AS BLOCK_TYPE
,COALESCE(svc.NAME, ser_sgn_blk.PROV_NAME, ogp.GROUP_NAME) RAW_BLOCK_NAME
,COALESCE((CASE WHEN @PedsVersion= 2 and oub.SERVICE_C IN ('260','270','280','290','300','310','320','330','500','600','610','620','630')  then 'Pediatrics' ELSE svc.NAME end), ser_sgn_blk.PROV_NAME, ogp.GROUP_NAME,'z_UNBLOCKED') BLOCK_NAME

,COALESCE(svc.NAME,'') SVC_BLOCK_NAME
,COALESCE(ser_sgn_blk.PROV_NAME,'') SRGN_BLOCK_NAME
,COALESCE(ogp.GROUP_NAME,'') GROUP_BLOCK_NAME
,oub.ROOM_ID
,rm.PROV_NAME ROOM_NAME
,COALESCE(lg_sgn.PROV_NAME,cs_sgn.PROV_NAME ) PHYSICIAN
,lg_sgn.PROV_NAME LOG_SURGEON
,cs_sgn.PROV_NAME CASE_SURGEON
,oub.SURGEON_ID
,ol.PRIMARY_PHYS_ID
,orc.OR_CASE_ID
,orc.PRIMARY_PHYSICIAN_ID CASE_PRIM_PHYS
,sgsvc.LOC_ID
,loc.LOCATION_ABBR LOCATION
,svc.NAME SURGICAL_SERVICE
,dd.HOLIDAY_YN 
,dd.WEEKEND_YN
INTO #BUTIL
FROM CLARITY.dbo.OR_UTIL_BLOCK oub
LEFT OUTER JOIN CLARITY.dbo.ZC_OR_SERVICE svc ON oub.SERVICE_C = svc.SERVICE_C
LEFT OUTER JOIN CLARITY.dbo.OR_GRP ogp ON oub.GROUP_ID = ogp.GROUP_ID
LEFT OUTER JOIN CLARITY.dbo.CLARITY_SER rm ON oub.ROOM_ID = rm.PROV_ID
LEFT OUTER JOIN CLARITY.dbo.CLARITY_SER ser_sgn_blk ON oub.SURGEON_ID = ser_sgn_blk.PROV_ID
LEFT OUTER JOIN CLARITY.dbo.OR_SER_SURG_SRVC sgsvc ON oub.ROOM_ID = sgsvc.PROV_ID
LEFT OUTER JOIN CLARITY.dbo.DATE_DIMENSION dd ON oub.SNAPSHOT_DATE = dd.CALENDAR_DT
LEFT OUTER JOIN CLARITY.dbo.CLARITY_LOC loc ON sgsvc.LOC_ID =loc.LOC_ID
LEFT OUTER JOIN CLARITY.dbo.ZC_SNAPSHOT_NUMBER n on oub.snapshot_number = n.snapshot_number
LEFT OUTER JOIN CLARITY.dbo.OR_LOG ol on oub.LOG_ID = ol.LOG_ID 
LEFT OUTER JOIN CLARITY.dbo.CLARITY_SER lg_sgn ON ol.PRIMARY_PHYS_ID = lg_sgn.PROV_ID
LEFT OUTER JOIN CLARITY.dbo.OR_CASE orc on oub.CASE_ID = orc.OR_CASE_ID
LEFT OUTER JOIN CLARITY.dbo.CLARITY_SER cs_sgn ON orc.PRIMARY_PHYSICIAN_ID = cs_sgn.PROV_ID
LEFT OUTER JOIN CLARITY.dbo.ZC_OR_SERVICE s on ol.SERVICE_C = s.SERVICE_C
WHERE 1=1
AND oub.SNAPSHOT_NUMBER  in (@PerfUtil,@SchUtil)
AND oub.SNAPSHOT_DATE >= @in_StartDate
AND oub.SNAPSHOT_DATE < DATEADD(D,1,@in_EndDate)
AND sgsvc.LOC_ID in (1071024300--UVHE Main OR
	, 1071035400 --UVBB Outpatient Surgery Center
	, 1071041900 --OCIR IVY ROAD OR
	, 1071036300 /*CVSL SURGICAL CARE RIVERSIDE OR*/
	, 1071029500 /*CPSA OR*/
	, 1071038800 /*PWMC OR*/
	, 1071074300 /*HYMC OR*/
	, 1071035402 /*CPSN OPSC OR*/  /*Added 6/6/2024 - Chris Meshes*/)
AND (0 IN (SELECT LOC_ID from @tab_Locs) OR sgsvc.LOC_ID in (SELECT LOC_ID from @tab_Locs))
AND ('0' IN (SELECT BLOCK_ID from @tab_Blocks) OR coalesce(oub.SERVICE_C,oub.GROUP_ID,oub.SURGEON_ID) IN (SELECT BLOCK_ID from @tab_Blocks))
AND dd.HOLIDAY_YN = 'N'
AND dd.WEEKEND_YN = 'N'


--select * from #BUTIL

/* MAIN OUTPUT */

SELECT 
CASE  WHEN g.BLOCK_NAME = 'z_UNBLOCKED' THEN NULL
	  WHEN g.USED IS NULL THEN 0
	  WHEN g.USED = 0 THEN 0
	  WHEN g.AVAILABLE IS NULL THEN 0
	  WHEN g.AVAILABLE = 0 THEN 0
	  ELSE (g.used *100.0)/g.AVAILABLE   END AS UTILIZATION_PCNT

,CASE  WHEN g.BLOCK_NAME = 'z_UNBLOCKED' THEN NULL
	  WHEN g.SCHEDULED_TO_USE IS NULL THEN 0
	  WHEN g.SCHEDULED_TO_USE = 0 THEN 0
	  WHEN g.AVAILABLE IS NULL THEN 0
	  WHEN g.AVAILABLE = 0 THEN 0
	  ELSE (g.SCHEDULED_TO_USE *100.0)/g.AVAILABLE   END AS SCHEDULED_PCNT
,g.* FROM 
	(
	SELECT
	 a.LOCATION
	,MAX(a.LOCATION_FULL_NAME) AS LOCATION_FULL_NAME
	,COALESCE(a.BLOCK_NAME, 'z_UNBLOCKED') AS BLOCK_NAME
	,CASE WHEN COALESCE(a.BLOCK_NAME, 'z_UNBLOCKED') = 'z_UNBLOCKED' THEN 'UNBLOCKED' ELSE a.BLOCK_NAME END AS BLOCK_DISPLAY_NAME
	,MAX(a.RAW_BLOCK_name) RAW_BLOCK_NAME
	,MAX(a.PEDIATRIC_BLOCK_NAME) PEDIATRIC_BLOCK_NAME
	,COUNT(DISTINCT b.SURGERY_ID) SURGERIES
	,COUNT(DISTINCT b.LOG_ID) LOGS
	,COUNT(DISTINCT b.CASE_ID) CASES

	,COALESCE(SUM(b.ACTUAL_IN_PROC_PROCEDURE_TIME) + SUM(b.ACTUAL_OVERBOOK_PROCEDURE_TIME) +SUM(b.ACTUAL_IN_BLOCK_SETUP_TIME),0) AS USED
	,COALESCE(MAX(a.AVAILABLE_MINUTES),0) AVAILABLE
	,COALESCE(MAX(r.SUM_MANUAL_RELEASED_MINUTES),0) MANUAL_RELEASED_MINUTES

	,COALESCE(SUM(b.ACTUAL_IN_PROC_PROCEDURE_TIME),0) AS IN_BLOCK_PROCEDURE_TIME
	,COALESCE(SUM(b.ACTUAL_OVERBOOK_PROCEDURE_TIME),0) AS IN_BLOCK_OVERBOOK
	,COALESCE(SUM(b.ACTUAL_IN_BLOCK_SETUP_TIME),0) AS IN_BLOCK_SETUP
	,COALESCE(SUM(b.ACTUAL_OUT_OF_BLOCK_PROCEDURE_TIME),0) AS OUT_OF_BLOCK_PROCEDURE
	,COALESCE(SUM(b.ACTUAL_OUT_OF_BLOCK_SETUP_TIME),0) AS OUT_OF_BLOCK_SETUP

	,COALESCE(SUM(b.SHEDULED_IN_PROC_PROCEDURE_TIME) + SUM(b.SHEDULED_OVERBOOK_PROCEDURE_TIME) +SUM(b.SHEDULED_IN_BLOCK_SETUP_TIME),0) AS SCHEDULED_TO_USE
	,COALESCE(SUM(b.SHEDULED_IN_PROC_PROCEDURE_TIME),0) AS SHEDULED_IN_PROC_PROCEDURE_TIME
	,COALESCE(SUM(b.SHEDULED_OVERBOOK_PROCEDURE_TIME),0) AS SHEDULED_OVERBOOK_PROCEDURE_TIME
	,COALESCE(SUM(b.SHEDULED_IN_BLOCK_SETUP_TIME),0) AS SHEDULED_IN_BLOCK_SETUP_TIME
	,COALESCE(SUM(b.SHEDULED_OUT_OF_BLOCK_PROCEDURE_TIME),0) AS SHEDULED_OUT_OF_BLOCK_PROCEDURE_TIME
	,COALESCE(SUM(b.SHEDULED_OUT_OF_BLOCK_SETUP_TIME),0) AS SHEDULED_OUT_OF_BLOCK_SETUP_TIME

	FROM #AVAIL a 
	LEFT OUTER JOIN #BUTIL b ON b.BLOCK_ID = a.BLOCK_ID AND b.LOCATION = a.LOCATION
	LEFT OUTER JOIN #RELD r ON b.BLOCK_NAME = r.BLOCK_NAME AND r.LOCATION =   b.LOCATION
	GROUP BY a.LOCATION ,a.BLOCK_NAME
	)g
ORDER BY g.BLOCK_NAME;


